plugins {
    id 'java'
    id 'maven-publish'
}

group = 'uz.sb'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'Chat lib'
                description = 'this is core for our chat project.'
                url = 'https://github.com/SardorBoboyev/ChatLibraries'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'SardorBoboyev'
                        name = 'Sardor Boboyev'
                        email = 'boboyevsardor350@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/SardorBoboyev/ChatLibraries'
                    developerConnection = 'scm:git:ssh://github.com/your-repo-url.git'
                    url = 'https://github.com/SardorBoboyev/ChatLibraries'
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/SardorBoboyev/ChatLibraries")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
            }
        }
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}